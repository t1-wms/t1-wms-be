plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

jacoco {
    toolVersion = "0.8.8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web:3.2.0'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:3.2.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.2.0'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.2.0'

    // MySQL
    implementation 'mysql:mysql-connector-java:8.0.32'
    implementation 'org.apache.tomcat:tomcat-jdbc:10.1.5'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
    implementation 'org.mybatis:mybatis:3.5.9'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.2.0'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // AWS S3
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.0'
    testImplementation 'org.springframework.security:spring-security-test:5.8.0'

    // JWT
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket:3.2.0'

    // Selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.1.2'

    // H2 Database (Runtime Only)
    runtimeOnly 'com.h2database:h2:2.1.212'
}

tasks.named('jar') {
    enabled = false
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs '-Xshare:off' // JVM 아규먼트 설정
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = false
        csv.required = false
    }

    // 결과 리포트에서 제외할 클래스들
    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            "**/*Application*",
                            "**/*Mapper*",
                            "**/*Provider*",
                            "**/*Config*",
                            "**/*Util*",
                            "**/*Request*",
                            "**/*Response*",
                            "**/*Entity*",
                            "**/common"
                    ] + Qdomains)
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    def Qdomains = []
    for (qPattern in '*.QA'..'*.QZ') {
        Qdomains.add(qPattern + '*')
    }

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            includes = []

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            excludes = [
                    "**.*Application*",
                    "**.*Mapper*",
                    "**.*Provider*",
                    "**.*Config*",
                    "**.*Request*",
                    "**.*Response*",
                    "**.*Entity*"
            ] + Qdomains
        }
    }
}
